plugins {
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
}

tasks.named('bootJar') {
    enabled = true
}

tasks.named('jar') {
    enabled = false
}

configurations {
    asciidoctorExt
}
ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation project(':core:core-enum')
    implementation project(':storage:redis-core')
    implementation project(':support:logging')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.security:spring-security-crypto:6.4.5'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.12.1'

    /*DB 오라클 접속에 필요한 라이브러리*/
    runtimeOnly 'com.oracle.database.jdbc:ojdbc8'
    implementation 'com.oracle.database.security:osdt_cert:21.3.0.0'
    implementation 'com.oracle.database.security:oraclepki:21.3.0.0'
    implementation 'com.oracle.database.security:osdt_core:21.3.0.0'

    runtimeOnly 'com.h2database:h2'
    testImplementation("com.navercorp.fixturemonkey:fixture-monkey-starter:1.1.15")

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //swagger
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15"

    // REST Docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Querydsl
    implementation "com.querydsl:querydsl-jpa:5.1.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
}

def qdslDir = "$buildDir/generated/sources/annotationProcessor/java/main"

sourceSets {
    main.java.srcDirs += [qdslDir]
}

tasks.named("clean") {
    doLast {
        file(qdslDir).deleteDir()
    }
}

test {
    outputs.dir snippetsDir
}
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test
}
bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}
